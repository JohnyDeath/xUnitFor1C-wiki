Начиная с версии 4.0 появилась возможность использовать сценарные тесты.

Это тесты, которые выполняются строго последовательно, начиная от первого шага и завершая последним шагом.
Если один из шагов падает, весь сценарий считается упавшим.

Для реализации подобного поведения шаги/тесты одного сценария объединяем в модуле (модуле обработки или модуле формы) одного файла-обработки тестирования (внешней или встроенной).
- В одном модуле может быть несколько тестовых сценариев.
- Тесты всегда будут запускаться в том порядке, который задан при добавлении тестов в методе `ЗаполнитьНаборТестов`
- Для метода начала тестового сценария нужно использовать первый тесты из тест.набора (в порядке добавления тестов в `ЗаполнитьНаборТестов`).
- Для метода завершения тестового сценария (например, освобождение ресурсов) рекомендуем использовать метод ПослеЗапускаТеста.
  - *не рекомендуется* использовать последний тестовый случай из набора тестов, т.к. возможны ошибки выполнения сценария
    - например. если в одном из шагов теста будут ошибки, все остальные шаги/тесты будут выполнены и до последнего теста дело не дойдет. Следовательно, операция завершения выполнена не будет.


Контекст сценария
---

Для сценарных тестов становится возможным использование контекста сценария.
2 метода работы с контекстом:
  * процедура `КонтекстЯдра.СохранитьКонтекст(НовыйКонтекст)`
    * устанавливает новое значение контекста для сценария
    * параметр `НовыйКонтекст` - значение нового контекста. Тип - произвольный.
    
  * функция `КонтекстЯдра.ПолучитьКонтекст()`
    * возвращает значение контекста сценария

  Использование методов работы с контекстом запрещено при использовании несценарных тестов.

Пример простого тестового сценария с использованием контекста
---

```
Перем КонтекстЯдра;
Перем Ожидаем;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	
	НаборТестов.НачатьГруппу("Тестовый сценарий", Истина);
	НаборТестов.Добавить("ТестДолжен_СохранитьКонтекст");
	НаборТестов.Добавить("ТестДолжен_ПроверитьСохраненныйКонтекст");

КонецПроцедуры

// { Сценарный тест
Процедура ТестДолжен_СохранитьКонтекст() Экспорт
	КонтекстЯдра.СохранитьКонтекст(10);
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСохраненныйКонтекст() Экспорт
	Контекст = КонтекстЯдра.ПолучитьКонтекст();
	Ожидаем.Что(Контекст).Равно(10);
КонецПроцедуры
// } 
```

Пример тестового сценария с параметризированными тестами
---

```
Перем КонтекстЯдра;
Перем Ожидаем;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	
	НаборТестов.НачатьГруппу("Тестовый сценарий с параметризированными тестами", Истина);
	НаборТестов.Добавить("ТестДолжен_СохранитьРезультатСложенияВКонтекст", НаборТестов.ПараметрыТеста(5, 7), "Тест должен сохранить результат сложения (5 + 7) в контекст");
	Тест = НаборТестов.Добавить("ТестДолжен_СравнитьКонтекстСоСвоимПараметром");
	Тест.Параметры.Добавить(12);
КонецПроцедуры

// { Тестовый сценарий с параметризированными тестами
Процедура ТестДолжен_СохранитьРезультатСложенияВКонтекст(Знач1, Знач2) Экспорт
	КонтекстЯдра.СохранитьКонтекст(Знач1 + Знач2);
КонецПроцедуры

Процедура ТестДолжен_СравнитьКонтекстСоСвоимПараметром(Результат) Экспорт
	Контекст = КонтекстЯдра.ПолучитьКонтекст();
	Ожидаем.Что(Контекст).Равно(Результат);
КонецПроцедуры
// } 
```