Версия 4.Х
---

Для тестирования данных с помощью макетов-образцов в продукте есть утверждения
- `ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениям`
- `ПроверитьРавенствоТаблиц` 

Схема тестирования при сравнении с образцом
---

- исходные данные (образец, таблица значений и т.п.) заранее сохраняются в макет (можно через ЗначениеВСтрокуВнутр, можно через преобразование в табличный документ или любым другим способом)
- в начале кода теста из макета получаем исходную таблицу данных
- выполняется основной код, получается таблица данных 
- далее обе таблицы просто сравниваются указанными утверждениями
- все :)

Пример кода сравнения с образцом (из [[Пример теста СКД и отчета, сделанного на компоновке]] )

[Код самого теста](https://github.com/xDrivenDevelopment/xUnitFor1C/blob/develop/src/Tests/Plugins/%D0%A2%D0%B5%D1%81%D1%82_%D0%9F%D1%80%D0%BE%D0%B2%D0%B5%D1%80%D0%B8%D1%82%D1%8C%D0%9E%D1%82%D1%87%D0%B5%D1%82%D0%9D%D0%B0%D0%A1%D0%BE%D0%BE%D1%82%D0%B2%D0%B5%D1%82%D1%81%D1%82%D0%B2%D0%B8%D0%B5%D0%AD%D1%82%D0%B0%D0%BB%D0%BE%D0%BD%D1%83/ObjectModule.txt) (модуль этой обработки) - приведен ниже по тексту. 

Код теста 
```
Перем КонтекстЯдра;
Перем Утверждения;
Перем ГенераторТестовыхДанных;
Перем УтвержденияПроверкаТаблиц;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
	ГенераторТестовыхДанных = КонтекстЯдра.Плагин("СериализаторMXL");
	УтвержденияПроверкаТаблиц = КонтекстЯдра.Плагин("УтвержденияПроверкаТаблиц");
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	НаборТестов.Добавить("Тест_ПроверитьОтчетНаСоответствиеЭталону");
КонецПроцедуры

Процедура Тест_ПроверитьОтчетНаСоответствиеЭталону() Экспорт
	Перем Расшифровка; // сюда будет передана информация расшифровки СКД, т.к. в эталоне она тоже есть
	
	// создание тестовых данных из табличного документа
	МакетТестовыеДанные = ПолучитьМакет("ТестовыеДанные");
	ТестовыеДанные = ГенераторТестовыхДанных.СоздатьДанныеПоТабличномуДокументу(МакетТестовыеДанные);
	
	Утверждения.ПроверитьБольше(ТестовыеДанные.Количество(), 0, "Ожидали, что загрузка макета загрузит данные больше 0");
	
	// Получение эталонного отчета
	// Важно эталон получать именно так из-за плясок с кодом языка макета
	МакетЭталона = ПолучитьМакет("Эталон");
	Эталон = Новый ТабличныйДокумент;
	Эталон.Вывести(МакетЭталона);
	
	// Формирование отчета
	РезультатОтчета = Новый ТабличныйДокумент;
	Отчет = Отчеты.ТестовыйОтчетСКДДляСравнениеСЭталоном.Создать();
	Отчет.СкомпоноватьРезультат(РезультатОтчета, Расшифровка);
	
	#Если НЕ (ТолстыйКлиентОбычноеПриложение или ТолстыйКлиентУправляемоеПриложение) Тогда

		УтвержденияПроверкаТаблиц.ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениям(Эталон, РезультатОтчета, "Ожидали, что эталон (слева) совпадет с результатом (справа), а они различны!");
		
	#Иначе
		// Сохранение в файлы mxl
		ИмяФайлаЭталона = ПолучитьИмяВременногоФайла("mxl");
		ИмяФайлаРезультатаОтчета = ПолучитьИмяВременногоФайла("mxl");
		Эталон.Записать(ИмяФайлаЭталона);
		РезультатОтчета.Записать(ИмяФайлаРезультатаОтчета);
			
		// Сравнение файлов
		
		// Если Истина, то будет показано окно демонстрации различий, если файлы различны
		// Использовать только для расследования ошибки, если тест падает на сравнении
		ПоказатьРазличия = Истина;
		
		// ПараметрыСравнения заполняется по необходимости. Если не заполнены, то используются значения по умолчанию платформы
		// Если не указан способ сравнения, будет попытка определить автоматически по расширению файла:
		//  mxl - ТабличныйДокумент
		//  txt,xml - ТекстовыйДокумент
		ПараметрыСравнения = Новый Структура;
		//ПараметрыСравнения.Вставить("ИгнорироватьПустоеПространство", Истина);
		//ПараметрыСравнения.Вставить("УчитыватьРазделителиСтрок", Ложь);
		//ПараметрыСравнения.Вставить("УчитыватьРегистр", Ложь);
		//ПараметрыСравнения.Вставить("СпособСравнения", СпособСравненияФайлов.ТекстовыйДокумент);
		
		Утверждения.ПроверитьРавенствоФайлов(ИмяФайлаЭталона, ИмяФайлаРезультатаОтчета,
			"Ожидали, что результат отчета соответствует эталону", ПараметрыСравнения, ПоказатьРазличия);
			
	#КонецЕсли 	
			
КонецПроцедуры
```

В версии 3.Х
---
Для тестирования данных с помощью макетов-образцов в продукте есть утверждения
- `ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениям`
- `ПроверитьРавенствоТаблиц` 

Схема тестирования остается [той же](https://github.com/xDrivenDevelopment/xUnitFor1C/wiki/%D0%A2%D0%B5%D1%81%D1%82%D0%B8%D1%80%D0%BE%D0%B2%D0%B0%D0%BD%D0%B8%D0%B5-%D1%87%D0%B5%D1%80%D0%B5%D0%B7-%D0%BE%D0%B1%D1%80%D0%B0%D0%B7%D0%B5%D1%86-%D0%B8%D1%81%D1%85%D0%BE%D0%B4%D0%BD%D1%8B%D1%85-%D0%B4%D0%B0%D0%BD%D0%BD%D1%8B%D1%85/#%D0%A1%D1%85%D0%B5%D0%BC%D0%B0-%D1%82%D0%B5%D1%81%D1%82%D0%B8%D1%80%D0%BE%D0%B2%D0%B0%D0%BD%D0%B8%D1%8F-%D0%BF%D1%80%D0%B8-%D1%81%D1%80%D0%B0%D0%B2%D0%BD%D0%B5%D0%BD%D0%B8%D0%B8-%D1%81-%D0%BE%D0%B1%D1%80%D0%B0%D0%B7%D1%86%D0%BE%D0%BC)

[Код самого теста для версии 3.Х](https://github.com/xDrivenDevelopment/xUnitFor1C/blob/develop/src/Tests/CommonApp/%D0%A2%D0%B5%D1%81%D1%82_%D0%9F%D1%80%D0%BE%D0%B2%D0%B5%D1%80%D0%B8%D1%82%D1%8C%D0%9E%D1%82%D1%87%D0%B5%D1%82%D0%9D%D0%B0%D0%A1%D0%BE%D0%BE%D1%82%D0%B2%D0%B5%D1%82%D1%81%D1%82%D0%B2%D0%B8%D0%B5%D0%AD%D1%82%D0%B0%D0%BB%D0%BE%D0%BD%D1%83/ObjectModule.txt) 
