Создание файлов-тестов
=======

Создаем новую внешнюю обработку, т.н. **тестовый набор**

В модуле этой обработки размещаются **тестовые случаи** (экспортные методы без параметров, чье имя может начинаться на `Тест` (*необязательно*))

Тестовый случай считается успешно выполненным, если в процессе его выполнения не было исключения.

Соответственно, для того, чтобы тест упал, нужно выбросить исключение 1С. Для этого используются вызовы специальных методов-утверждений из фреймворка `xUnitFor1C`. См. [Методы проверки/утверждений](https://github.com/xDrivenDevelopment/xUnitFor1C/wiki/%D0%9C%D0%B5%D1%82%D0%BE%D0%B4%D1%8B-%D0%BF%D1%80%D0%BE%D0%B2%D0%B5%D1%80%D0%BA%D0%B8---%D1%83%D1%82%D0%B2%D0%B5%D1%80%D0%B6%D0%B4%D0%B5%D0%BD%D0%B8%D1%8F)

Примеры тестов:
---

- TODO [пример сценарного теста](пример сценарного теста)

- [простой тест для обычной формы](https://github.com/xDrivenDevelopment/xUnitFor1C/blob/develop_v4_reborn/src/Tests/Plugins/%D0%A2%D0%B5%D1%81%D1%82%D1%8B%D0%9F%D1%80%D0%BE%D0%B2%D0%B5%D1%80%D0%B8%D1%82%D1%8C%D0%A0%D0%B0%D0%B2%D0%B5%D0%BD%D1%81%D1%82%D0%B2%D0%BE%D0%A2%D0%B0%D0%B1%D0%BB%D0%B8%D1%86/ObjectModule.txt)

- [этот же простой тест для управляемой формы](https://github.com/xDrivenDevelopment/xUnitFor1C/blob/develop_v4_reborn/src/Tests/Plugins/%D0%A2%D0%B5%D1%81%D1%82%D1%8B%D0%9F%D1%80%D0%BE%D0%B2%D0%B5%D1%80%D0%B8%D1%82%D1%8C%D0%A0%D0%B0%D0%B2%D0%B5%D0%BD%D1%81%D1%82%D0%B2%D0%BE%D0%A2%D0%B0%D0%B1%D0%BB%D0%B8%D1%86/Form/%D0%A4%D0%BE%D1%80%D0%BC%D0%B0/%D0%A4%D0%BE%D1%80%D0%BC%D0%B0.txt)

- TODO [примеры параметрических тестов для ОФ](https://github.com/xDrivenDevelopment/xUnitFor1C/blob/develop/src/Tests/selftests/%D0%A2%D0%B5%D1%81%D1%82_%D0%9F%D0%B0%D1%80%D0%B0%D0%BC%D0%B5%D1%82%D1%80%D1%8B%D0%A2%D0%B5%D1%81%D1%82%D0%BE%D0%B2/ObjectModule.txt)

- TODO [такой же пример параметрических тестов для УФ](https://github.com/xDrivenDevelopment/xUnitFor1C/blob/develop/src/Tests/selftests/%D0%A2%D0%B5%D1%81%D1%82_%D0%9F%D0%B0%D1%80%D0%B0%D0%BC%D0%B5%D1%82%D1%80%D1%8B%D0%A2%D0%B5%D1%81%D1%82%D0%BE%D0%B2/Form/%D0%A4%D0%BE%D1%80%D0%BC%D0%B0/%D0%A4%D0%BE%D1%80%D0%BC%D0%B0.txt)

Предопределенные методы теста
---
  * Переменная `КонтекстЯдра`
    * содержит в себе ссылку на экземпляр фреймворка `xUnitFor1C` - объект обработки `xddTestRunner`
    * с помощью этой переменной можно использовать предопределенные методы `xUnitFor1C`. Например, см. [Методы проверки/утверждений](https://github.com/xDrivenDevelopment/xUnitFor1C/wiki/%D0%9C%D0%B5%D1%82%D0%BE%D0%B4%D1%8B-%D0%BF%D1%80%D0%BE%D0%B2%D0%B5%D1%80%D0%BA%D0%B8---%D1%83%D1%82%D0%B2%D0%B5%D1%80%D0%B6%D0%B4%D0%B5%D0%BD%D0%B8%D1%8F)
    * имя `КонтекстЯдра` необязательно, можно использовать любое другое имя переменной
    * значение этой переменной должно быть заполнено в методе `Инициализация`

    
  * Процедура `Инициализация`
      * должна инициализировать контекст ядра
      * должна заполнить значение переменной `КонтекстЯдра`
      * должна заполнить значение переменных других плагинов, например, `Ожидаем` или `Утверждения`
      * один входной параметр `КонтекстЯдраПараметр` - ссылка на экземпляр фреймворка `xUnitFor1C` - объект обработки xddTestRunner


  * Процедура `ЗаполнитьНаборТестов`
    * заполняет набор описаний тестовых случаев и тестовых сценариев 
    * именно этот набор и является списком выполняемых тестов/сценариев
    * один входной параметр - объект НаборТестов, который используется для добавления тестов в различных режимах. [Смотрите API НаборТестов](https://github.com/xDrivenDevelopment/xUnitFor1C/wiki/%D0%A1%D0%BE%D0%B7%D0%B4%D0%B0%D0%BD%D0%B8%D0%B5-%D1%84%D0%B0%D0%B9%D0%BB%D0%BE%D0%B2-%D1%82%D0%B5%D1%81%D1%82%D0%BE%D0%B2/_edit#%D0%9E%D0%BF%D0%B8%D1%81%D0%B0%D0%BD%D0%B8%D0%B5-api-%D0%9D%D0%B0%D0%B1%D0%BE%D1%80%D0%A2%D0%B5%D1%81%D1%82%D0%BE%D0%B2)


  * *функция* `ПолучитьСписокТестов` (рекомендуется использование `ЗаполнитьНаборТестов`)
    * должна вернуть массив описаний тестовых случаев
    * именно этот массив и является списком выполняемых тестов
    * функция без параметров


  - процедура `ПередЗапускомТеста` (*необязательная*)
    - вызывается *перед запуском каждого теста*  


  - процедура `ПослеЗапускаТеста` (*необязательная*)
    - вызывается *после запуска каждого теста*
    - метод вызывается независимо от того, успешно прошел тест или упал.

Базовые приемы написания тестов
---
Для гарантирования независимости тестов (это одно из основных требований к тестам) рекомендуется использовать **механизм транзакций**.
    
Перед запуском теста начинаем транзакцию, после выполнения теста транзакция отменяется. В итоге в базе не остается никаких следов тестирования и тесты работают независимо.

Можно использовать следующий шаблон методов теста
```sh
Процедура ПередЗапускомТеста() Экспорт
	НачатьТранзакцию();
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	Если ТранзакцияАктивна() Тогда
	    ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры
```

Базовый шаблон файла-теста (серверный тест в модуле обработки)
---
```sh
Перем КонтекстЯдра;
Перем Утверждения;

//{ основная процедура для юнит-тестирования xUnitFor1C
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	НаборТестов.Добавить("ТестДолжен_СложитьДваИДва");
КонецПроцедуры

//}

//{ Блок юнит-тестов

Процедура ПередЗапускомТеста() Экспорт
	НачатьТранзакцию();
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	Если ТранзакцияАктивна() Тогда
	    ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

Процедура ТестДолжен_СложитьДваИДва() Экспорт
	Утверждения.ПроверитьРавенство(2 + 2, 4, "2 + 2, 4");
КонецПроцедуры

//}
```

API НаборТестов
---

  * Функция `Добавить(Знач ИмяМетода, Знач Параметры = Неопределено, Знач Представление = "")`
      * добавляет тестовый случай в набор тестов
      * `ИмяМетода` должно указывать на **экспортную процедуру из текущего модуля**. Количество параметров этой процедуры должно совпадать с количеством параметров из второго параметра `Параметры`. Если `Параметры` не заданы, процедура без параметров.
      * `Параметры` (*необязательно*) - параметры тестового случая. Задаются только на этапе создания набора тестов. Параметры теста можно задать с помощью метода `ПараметрыТеста'.
      * `Представление` (*необязательно*) - представление тестового случая в окне браузера тестов.
      
      
  * Процедура `НачатьГруппу(Знач ИмяГруппы, Знач СтрогийПорядокВыполнения = Ложь)`
      * создать группу тестов. Удобно для выполнения сценариев, т.е. последовательных шагов тестов.
      
  * Функция `ПараметрыТеста(Знач Парам1, Знач Парам2 = Неопределено, Знач Парам3 = Неопределено, Знач Парам4 = Неопределено, Знач Парам5 = Неопределено, Знач Парам6 = Неопределено, Знач Парам7 = Неопределено, Знач Парам8 = Неопределено, Знач Парам9 = Неопределено)`
      * возможно задания определенных параметров для тестового случая.
  
  * Процедура `СлучайныйПорядокВыполнения` - определяет возмжожность случайного выполнения тестов в группе тестов. Удобно для несвязанных тестов.

  * Процедура `СтрогийПорядокВыполнения` - определяет возмжожность случайного выполнения тестов в группе тестов. Удобно для сценарных тестов, которые должны выполняться строго последовательно друг за другом.


Использование параметрических тестов
---

Пример теста:

```
Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	НаборТестов.НачатьГруппу("Группа параметризованных тестов со строгим порядком выполнения", Истина);

	НаборТестов.Добавить("ТестДолжен_СохранитьРезультатСложенияВКонтекст", 
НаборТестов.ПараметрыТеста(5, 7), "Тест должен сохранить результат сложения (5 + 7) в контекст");

	Тест = НаборТестов.Добавить("ТестДолжен_СравнитьКонтекстСоСвоимПараметром");
	Тест.Параметры.Добавить(12);
КонецПроцедуры
```

```
// { Группа параметризованных тестов со строгим режимом выполнения
Процедура ТестДолжен_СохранитьРезультатСложенияВКонтекст(Знач1, Знач2) Экспорт
	КонтекстЯдра.СохранитьКонтекст(Знач1 + Знач2);
КонецПроцедуры

Процедура ТестДолжен_СравнитьКонтекстСоСвоимПараметром(Результат) Экспорт
	Контекст = КонтекстЯдра.ПолучитьКонтекст();
	Ожидаем.Что(Контекст).Равно(Результат);
КонецПроцедуры
// } Группа параметризованных тестов со строгим режимом выполнения
```

[Код теста xUnitFor1C с параметрами](https://github.com/xDrivenDevelopment/xUnitFor1C/blob/develop_v4_reborn/src/Tests/Plugins/%D0%A2%D0%B5%D1%81%D1%82%D1%8B_%D0%97%D0%B0%D0%B3%D1%80%D1%83%D0%B7%D1%87%D0%B8%D0%BA%D0%A4%D0%B0%D0%B9%D0%BB%D0%B0_%D0%9D%D0%BE%D0%B2%D1%8B%D0%B9API%D0%9E%D0%B1%D1%8A%D1%8F%D0%B2%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F%D0%A2%D0%B5%D1%81%D1%82%D0%BE%D0%B2/ObjectModule.txt#L17)
