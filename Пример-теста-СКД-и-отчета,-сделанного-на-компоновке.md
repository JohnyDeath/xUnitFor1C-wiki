Простой пример теста проверки отчета на СКД. 

В тесте сначала создаются тестовые данные в базе, затем выполняется отчет на СКД, получается сводная таблица. 
Эта таблица сверяется с эталоном. 

[Можно скачать сам файл теста](https://github.com/xDrivenDevelopment/xUnitFor1C/blob/develop/Tests/CommonApp/%D0%A2%D0%B5%D1%81%D1%82_%D0%9F%D1%80%D0%BE%D0%B2%D0%B5%D1%80%D0%B8%D1%82%D1%8C%D0%9E%D1%82%D1%87%D0%B5%D1%82%D0%9D%D0%B0%D0%A1%D0%BE%D0%BE%D1%82%D0%B2%D0%B5%D1%82%D1%81%D1%82%D0%B2%D0%B8%D0%B5%D0%AD%D1%82%D0%B0%D0%BB%D0%BE%D0%BD%D1%83.epf) (это внешняя обработка )

[Код самого теста](https://github.com/xDrivenDevelopment/xUnitFor1C/blob/develop/src/Tests/CommonApp/%D0%A2%D0%B5%D1%81%D1%82_%D0%9F%D1%80%D0%BE%D0%B2%D0%B5%D1%80%D0%B8%D1%82%D1%8C%D0%9E%D1%82%D1%87%D0%B5%D1%82%D0%9D%D0%B0%D0%A1%D0%BE%D0%BE%D1%82%D0%B2%D0%B5%D1%82%D1%81%D1%82%D0%B2%D0%B8%D0%B5%D0%AD%D1%82%D0%B0%D0%BB%D0%BE%D0%BD%D1%83/ObjectModule.txt) (модуль этой обработки) - приведен ниже по тексту. 

Настройка схемы компоновки в отчете

![image](https://cloud.githubusercontent.com/assets/2920817/6205174/8d75c6a6-b576-11e4-997e-c19129f0b7c9.png)

![image](https://cloud.githubusercontent.com/assets/2920817/6205169/7170fe80-b576-11e4-8aba-91a37a3d4919.png)

Эталон результата работы СКД (на базе указанной выше настройки)

![image](https://cloud.githubusercontent.com/assets/2920817/6205108/c53d49f4-b573-11e4-944a-325d0de6fb4d.png)

Макет данных для теста

![image](https://cloud.githubusercontent.com/assets/2920817/6205140/19a30244-b575-11e4-9a83-ba2bf273a4ce.png)


Код теста (из http://goo.gl/6z4H6A)
```
Перем юТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	юТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("Тест_ПроверитьОтчетНаСоответствиеЭталону");
	Возврат ВсеТесты;
	
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
	НачатьТранзакцию();
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	Если ТранзакцияАктивна() Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

Процедура Тест_ПроверитьОтчетНаСоответствиеЭталону() Экспорт
	Перем Расшифровка; // сюда будет передана информация расшифровки СКД, т.к. в эталоне она тоже есть
	
	// создание тестовых данных из табличного документа
	МакетТестовыеДанные = ПолучитьМакет("ТестовыеДанные");
	ТестовыеДанные = юТест.СоздатьДанныеПоТабличномуДокументу(МакетТестовыеДанные);
		
	// Получение эталонного отчета
	// Важно эталон получать именно так из-за плясок с кодом языка макета
	МакетЭталона = ПолучитьМакет("Эталон");
	Эталон = Новый ТабличныйДокумент;
	Эталон.Вывести(МакетЭталона);
	
	// Формирование отчета
	РезультатОтчета = Новый ТабличныйДокумент;
	Отчет = Отчеты.ТестовыйОтчетСКДДляСравнениеСЭталоном.Создать();
	Отчет.СкомпоноватьРезультат(РезультатОтчета, Расшифровка);
	
	#Если ТолстыйКлиентОбычноеПриложение или ТолстыйКлиентУправляемоеПриложение Тогда

		// Сохранение в файлы mxl
		ИмяФайлаЭталона = ПолучитьИмяВременногоФайла("mxl");
		ИмяФайлаРезультатаОтчета = ПолучитьИмяВременногоФайла("mxl");
		Эталон.Записать(ИмяФайлаЭталона);
		РезультатОтчета.Записать(ИмяФайлаРезультатаОтчета);
			
		// Сравнение файлов
		
		// Если Истина, то будет показано окно демонстрации различий, если файлы различны
		// Использовать только для расследования ошибки, если тест падает на сравнении
		ПоказатьРазличия = Истина;
		
		ПараметрыСравнения = Новый Структура;
		юТест.ПроверитьРавенствоФайлов(ИмяФайлаЭталона, ИмяФайлаРезультатаОтчета,
			"Ожидали, что результат отчета соответствует эталону", ПараметрыСравнения, ПоказатьРазличия);

	#Иначе
		юТест.ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениям(Эталон, РезультатОтчета, "Ожидали, что эталон (слева) совпадет с результатом (справа), а они различны!");
			
	#КонецЕсли 	
	
КонецПроцедуры

```