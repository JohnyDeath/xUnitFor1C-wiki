Введение
---

Рекомендуем почитать статью автора реализации этого вида утверждений
* [Юнит-тесты, BDD и сила текучих утверждений (fluent assertions) в 1С](http://habrahabr.ru/post/260013/)
    - 5 минут на прочтение

Обращение к плагину (пример)
----

```
Перем КонтекстЯдра;
Перем Ожидаем;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
КонецПроцедуры
```

API
---


+ `Что(ПроверяемоеЗначение, Сообщение = "")` — сохраняет в контексте внешней обработки проверяемое значение и дополнительное сообщение для исключений выбрасываемых утверждениями.

+ `Не_()` — отрицает любое утверждение следующее по цепи.

+ `ЭтоИстина()` — утверждает, что проверяемое значение является Истиной.

+ `ЭтоЛожь()` — утверждает, что проверяемое значение является Ложью.

+ `Равно(ОжидаемоеЗначение)` — утверждает, что проверяемое значение равно ожидаемому.

+ `Больше(МеньшееЗначение)` — утверждает, что проверяемое значение больше, чем переданное в утверждение.

+ `БольшеИлиРавно(МеньшееИлиРавноеЗначение)` / `Минимум(МинимальноеЗначение)` — утверждает, что проверяемое значение больше или равно переданному в утверждение.

+ `МеньшеИлиРавно(БольшееИлиРавноеЗначение)` / `Максимум(МаксимальноеЗначение)` — утверждает, что проверяемое значение меньше или равно переданному в утверждение.

+ `Меньше(БольшееЗначение)` — утверждает, что проверяемое значение меньше, чем переданное в утверждение.

+ `Заполнено()` — утверждает, что проверяемое значение отличается от значения по умолчанию того же типа.

+ `Существует()` — утверждает, что проверяемое значение не Null и не Неопределено.

+ `ЭтоНеопределено()` — утверждает, что проверяемое значение это Неопределено.

+ `ЭтоNull()` — утверждает, что проверяемое значение это Null.

+ `ИмеетТип(Тип)` — утверждает, что проверяемое значение имеет переданный в утверждение тип или имя типа.

+ `Между(НачальноеЗначение, КонечноеЗначение)` — утверждает, что проверяемое значение находится между переданными в утверждение значениями.

+ `Содержит(ИскомоеЗначение)` — утверждает, что проверяемое значение содержит переданное в утверждение. Применяется для строк и коллекций.

+ `ИмеетДлину(ОжидаемаяДлина)` — утверджает, что проверяемое значение имеет длину переданную в утверждение. Применяется для строк и коллекций.

Примеры использования
---

```
Ожидаем.Что(1 > 0).ЭтоИстина();
Ожидаем.Что(13 = 2).Не_().ЭтоИстина();
Ожидаем.Что(5 = 7).ЭтоЛожь();
Ожидаем.Что(5).Равно(5);
Ожидаем.Что(4).Больше(2);
Ожидаем.Что(7).БольшеИлиРавно(7);
Ожидаем.Что(НекийМассив.Количество()).Минимум(9);
Ожидаем.Что(90).МеньшеИлиРавно(100);
Ожидаем.Что(СтрДлина(НекаяСтрока)).Максимум(90);
Ожидаем.Что(55).Меньше(56);
Ожидаем.Что(1).Заполнено();
Ожидаем.Что(Новый Массив).Существует();
Ожидаем.Что(Неопределено).ЭтоНеопределено();
Ожидаем.Что(ВыборкаИзБД.НекоеПоле).ЭтоNull();
Ожидаем.Что("").ИмеетТип("Строка");
Ожидаем.Что(7).Между(1, 10);
Ожидаем.Что("Некая строка").Содержит("стр");
Ожидаем.Что("Некая строка").ИмеетДлину(12);
```

Пример немного сложнее:
---

```
Ожидаем.Что("Некая строка")
		.Существует()
		.Не_().ИмеетТип("Число")
		.ИмеетДлину(12)
		.Не_().Содержит("!!!");
```